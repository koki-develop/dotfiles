# !/bin/zsh -e

zmodload zsh/zprof

#
# Options
#

setopt auto_cd
setopt magic_equal_subst
setopt hist_ignore_dups
setopt no_beep
setopt no_hist_beep
setopt no_list_beep

#
# Environment variables
#

export PATH="${HOME}/.local/bin:${PATH}"
export PATH="${HOME}/.config/v-analyzer/bin:${PATH}"
export PATH="${HOME}/scripts:${PATH}"
export GPG_TTY=$(tty)
export EDITOR='nvim'

#
# Homebrew
#

eval "$(/opt/homebrew/bin/brew shellenv)"
BREW_PREFIX=$(brew --prefix)

#
# Styles
#

zstyle ':completion:*:default' menu select=2

#
# Set up zinit
#

ZINIT_HOME="${HOME}/.local/share/zinit/zinit.git"
source "${ZINIT_HOME}/zinit.zsh"

# Load plugins (turbo mode)
zinit ice wait lucid
zinit light "zsh-users/zsh-syntax-highlighting"
zinit ice wait lucid
zinit light "zsh-users/zsh-autosuggestions"
zinit ice wait lucid
zinit light "azu/ni.zsh"

#
# Set up mise
#

export MISE_GLOBAL_CONFIG_FILE="${HOME}/.local/share/chezmoi/mise.toml"
eval "$(mise activate zsh)"

#
# Completion
#

fpath=("${HOME}/.zsh/completion" $fpath)
autoload bashcompinit && bashcompinit
autoload -Uz compinit
if [[ -n ${ZDOTDIR:-$HOME}/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi
zinit cdreplay -q

for file in ~/.local/share/chezmoi/completions/*.kdl; do
  . <(usage generate completion zsh --file "$file" "$(basename "$file" .kdl)")
done

#
# Key bindings
#

if [[ $- == *i* ]]; then
  # edit-command-line: Edit command in $EDITOR
  autoload -U edit-command-line
  zle -N edit-command-line
  bindkey '^O' edit-command-line

  bindkey '^P' up-line-or-history
  bindkey '^N' down-line-or-history

  bindkey '^A' beginning-of-line
  bindkey '^E' end-of-line

  bindkey '^F' forward-char
  bindkey '^B' backward-char

  bindkey '^R' history-incremental-search-backward

  bindkey '^D' delete-char-or-list

  # ghq repository selector widget
  function _ghq_cd_widget() {
    REPO=$(gh q ls | gofzf --case-sensitive)
    if [ -n "${REPO}" ]; then
      cd "${GHQ_ROOT}/${REPO}"
    fi
    zle reset-prompt
  }
  zle -N _ghq_cd_widget
  bindkey '^G' _ghq_cd_widget
fi

#
# Set up tools
#

# atuin
eval "$(atuin init zsh)"

# direnv
eval "$(direnv hook zsh)"

# aws-cli
complete -C "$(which aws_completer)" aws

# ollamit
export OLLAMIT_MODEL=llama3:8b

# gcloud
source "${BREW_PREFIX}/share/google-cloud-sdk/path.zsh.inc"
source "${BREW_PREFIX}/share/google-cloud-sdk/completion.zsh.inc"

# mysql
export PATH="${BREW_PREFIX}/opt/mysql-client/bin:$PATH"

# libpq
export PATH="${BREW_PREFIX}/opt/libpq/bin:$PATH"

# go
export PATH="${HOME}/go/bin:${PATH}"

# java
export PATH="${BREW_PREFIX}/opt/openjdk/bin:$PATH"

# terraform
complete -o nospace -C "$(which terraform)" terraform

# gh-q
export GHQ_ROOT="${HOME}/work/repos"
export GHQ_SSH_KEY_PATH="${HOME}/.ssh/github"

#
# Aliases
#

# sounds
alias blow='afplay /System/Library/Sounds/Blow.aiff'
alias sosumi='afplay /System/Library/Sounds/Sosumi.aiff'

function ws() {
eval "${(q)@}"
local exit_code=$?
  if [ $exit_code -eq 0 ]; then
    blow
  else
    sosumi
  fi
  return $exit_code
}

# aws
alias av='aws-vault'
# docker
alias d='docker'
alias dc='docker compose'
# cat
alias cat='gat --mask-secrets'
function gess() {
  gat --force-color "$@" | less -R
}
# cd
alias ..2='cd ../..'
alias ..3='cd ../../..'
alias ..4='cd ../../../..'
alias cdd='cd ~/.local/share/chezmoi'
alias cdw="cd ${HOME}/work"
# chezmoi
alias ch='chezmoi'
# diff
alias diff='colordiff'
# git
alias g='git'
alias gad='git add'
alias gbr='git branch'
alias gcom='git commit -m'
alias gdi='git diff'
alias gdem="git branch --merged | egrep -v '\*|develop|master|main' | xargs git branch --delete"
alias gfe='git fetch'
alias glo='git log --pretty=oneline'
alias gnow='git commit --all --message "commit"'
alias gre='git reset'
alias grest='git restore'
alias gst='git status'
function gsw() {
  if [ $# -gt 0 ]; then
    git switch "$@"
    return
  fi

  local branch=$(git branch --all | sed 's/^[* ] //; s|remotes/[^/]*/||' | grep -v -- '->' | sort -u | gofzf)
  if [ -n "${branch}" ]; then
    git switch "${branch}"
  fi
}
function gwkt() {
  case "$1" in
    add)
      if [[ -z "$2" ]]; then
        echo "Usage: gwkt add <branch_name>"
        return 1
      fi
      local branch_name="$2"
      git worktree add .git/gwkt/worktrees/"$branch_name" -b "$branch_name"
      ;;
    rm)
      if [[ -z "$2" ]]; then
        echo "Usage: gwkt rm <branch_name>"
        return 1
      fi
      local branch_name="$2"
      git worktree remove .git/gwkt/worktrees/"$branch_name"
      ;;
    ls)
      git worktree list --porcelain | grep "^worktree $(pwd)/.git/gwkt/worktrees" | sed "s|^worktree $(pwd)/.git/gwkt/worktrees/||"
      ;;
    cd)
      if [[ -z "$2" ]]; then
        echo "Usage: gwkt cd <branch_name>"
        return 1
      fi
      local branch_name="$2"
      local worktree_path=".git/gwkt/worktrees/$branch_name"
      if [[ -d "$worktree_path" ]]; then
        cd "$worktree_path"
      else
        echo "Worktree '$branch_name' does not exist"
        return 1
      fi
      ;;
    *)
      echo "Usage: gwkt {add|rm|ls|cd} <branch_name>"
      return 1
      ;;
  esac
}
# ls
alias ls='eza -1 --icons always'
alias l='ls'
alias la='ls -a'
alias ll='ls -l'
alias lla='ls -la'
alias tree='ls --tree --git-ignore'
# mkdir
alias mkdir='mkdir -p'
# mv
alias mv='mv -i'
# rg
alias rg='rg --ignore-case --hidden'
# rm
alias rm='gotrash put --rm-mode'
# sed
alias sed='gsed'
# terraform
alias tf='terraform'
# vim
alias vi='nvim'
alias vim='nvim'

#
# Auto-start tmux
#

if [[ -z "$TMUX" ]] && [[ $- == *i* ]]; then
  tmux attach-session -t main 2>/dev/null || tmux new-session -s main
else
  # starship
  eval "$(starship init zsh)"
fi

zprof
